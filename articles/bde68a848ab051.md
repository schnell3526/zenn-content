---
title: "Oracle Cloud Infrastructure ã‚’ Terraform ç®¡ç†ã§ãã‚‹ã‚ˆã†ã«ã™ã‚‹ã¾ã§"
emoji: "ğŸ¡"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["terraform", "IaC", "OCI"]
published: true
---

schnell ã¨ç”³ã—ã¾ã™. ç¾åœ¨ SaaS ç³»ä¼æ¥­ã§ MLOps ã‚’æ¨é€²ã™ã‚‹ã‚¨ãƒ³ã‚¸ãƒ‹ã‚¢ã¨ã—ã¦å‹¤å‹™ã—ã¦ã„ã¾ã™.
æœ¬è¨˜äº‹ã§ã¯ã€è‡ªä¸»å­¦ç¿’ã®ä¸€ç’°ã¨ã—ã¦å–ã‚Šçµ„ã‚“ã§ã„ã‚‹ Oracle Cloud Infrastructureï¼ˆä»¥ä¸‹ã€OCIï¼‰ã®ç’°å¢ƒã‚’ Terraform ã§ç®¡ç†ã™ã‚‹ãŸã‚ã®æ‰‹é †ã«ã¤ã„ã¦ã€å‚™å¿˜éŒ²ã¨ã—ã¦å…±æœ‰ã„ãŸã—ã¾ã™.

OCI ã¯ AWS ã‚„ GCP ã¨ã„ã£ãŸæœ‰å IaaS ã‚µãƒ¼ãƒ“ã‚¹ã¨æ¯”ã¹ã‚‹ã¨åˆ©ç”¨ç¤¾ãŒå°‘ãªãåœ°å‘³ã§ã™ãŒç„¡æ–™æ ãŒéå¸¸ã«å……å®Ÿã—ã¦ãŠã‚Š
ä¾‹ãˆã°ã€AWSã®EC2ã«ç›¸å½“ã™ã‚‹ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ãƒ†ã‚£ãƒ³ã‚°ãƒªã‚½ãƒ¼ã‚¹ã§ã¯ Arm ç‰ˆã‚’é¸æŠã™ã‚‹ã“ã¨ã§ RAM 24GBã€4 OPCU ã®ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ãŒç„¡æ–™ã§åˆ©ç”¨ã§ãã¾ã™.

ã“ã®ãƒªã‚½ãƒ¼ã‚¹ã¯æœ€å¤§ 4 ã¤ã® VM ã«åˆ†å‰²å¯èƒ½ã§ã€**ç„¡æ–™æ ã®ç¯„ç–‡ã§** RAM 6GBã€1 OCPU ã®ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ã‚’4å°ä½œæˆã™ã‚‹ã“ã¨ãŒã§ã, ç°¡æ˜“çš„ãª kubernetes ã‚¯ãƒ©ã‚¹ã‚¿ã§ã•ãˆã‚‚é‹ç”¨ã™ã‚‹ã“ã¨ãŒååˆ†ã«å¯èƒ½ãªã‚¹ãƒšãƒƒã‚¯ã¨ãªã£ã¦ã„ã¾ã™.

å°šã€æœ¬è¨˜äº‹ã§ã¯ãŠãã‚‰ãæœ€é›£é–¢ã§ã‚ã‚ã† OCI ã«ã‚µã‚¤ãƒ³ã‚¢ãƒƒãƒ—ã™ã‚‹ã¨ã„ã†ãƒ—ãƒ­ã‚»ã‚¹ã«ã¤ã„ã¦ã¯çœç•¥ã—ã¾ã™ã€‚

# å®Ÿè¡Œç’°å¢ƒã«ã¤ã„ã¦

macOS Sonoma ã§å®Ÿè¡Œç¢ºèªã‚’ã—ã¦ãŠã‚Šã¾ã™.

```bash
â¯ sw_vers
ProductName:            macOS
ProductVersion:         14.6.1
BuildVersion:           23G93
```

terraform ã¯ 1.10.4 ã‚’åˆ©ç”¨ã—ã¾ã™.
```bash
â¯ terraform -v
Terraform v1.10.4
on darwin_arm64
```

# API ã‚­ãƒ¼ã®è¿½åŠ 

ãƒ—ãƒ­ã‚°ãƒ©ãƒ  (terraform) ã‹ã‚‰ OCI ã«ã‚¢ã‚¯ã‚»ã‚¹ã™ã‚‹ãŸã‚ã« API ã‚­ãƒ¼ã®ç™»éŒ²ã‚’è¡Œã„ã¾ã™.
ä»¥ä¸‹ã®ã‚³ãƒãƒ³ãƒ‰ã‚’ã‚’å®Ÿè¡Œã™ã‚‹ã“ã¨ã§ã€API ã‚­ãƒ¼ã¨ã—ã¦ç™»éŒ²ã™ã‚‹ RSA ã‚­ãƒ¼ã‚’ä½œæˆã—ã¦ã€å…¬é–‹éµã‚’ã‚¯ãƒªãƒƒãƒ—ãƒœãƒ¼ãƒ‰ã«ã‚³ãƒ”ãƒ¼ã§ãã¾ã™.

```bash
mkdir ~/.oci && cd ~/.oci
openssl genrsa -out oci_rsa.pem 2048
chmod 600 oci_rsa.pem
openssl rsa -pubout -in oci_rsa.pem -out oci_rsa_pub.pem
cat oci_rsa_pub.pem | pbcopy
```

æ¬¡ã«ã‚³ãƒ³ã‚½ãƒ¼ãƒ«ã«ãƒ­ã‚°ã‚¤ãƒ³ã—ã¦ API ã‚­ãƒ¼ã®ç™»éŒ²ã‚’è¡Œã„ã¾ã™.

ã“ã¡ã‚‰ã®[ãƒªãƒ³ã‚¯](https://cloud.oracle.com/identity/domains/my-profile/api-keys)ã«é£›ã‚“ã§ã„ãŸã ãã€ã€ŒAPI ã‚­ãƒ¼ã®è¿½åŠ ã€->ã€Œå…¬é–‹ã‚­ãƒ¼ã®è²¼ä»˜ã‘ã€ã‚’é¸æŠã—ã¦å…ˆã»ã©ã‚¯ãƒªãƒƒãƒ—ãƒœãƒ¼ãƒ‰ã«ã‚³ãƒ”ãƒ¼ã—ãŸå…¬é–‹éµã‚’è¿½åŠ ã—ã¦ãã ã•ã„.

ä»¥ä¸Šã§ API ã‚­ãƒ¼ã®è¿½åŠ ã¯å®Œäº†ã§ã™.

# terraform ã® oci provider è¨­å®šã«å¿…è¦ãªæƒ…å ±ã‚’é›†ã‚ã‚‹

OCI ã®ã‚³ãƒ³ã‚½ãƒ¼ãƒ«ã‹ã‚‰ä»¥ä¸‹ã®æƒ…å ±ã‚’é›†ã‚ã¾ã™

1. Tenancy OCID
[ã“ã“](https://cloud.oracle.com/tenancy)ã‹ã‚‰å–å¾—ã—ã¦ãã ã•ã„.
`ocid1.tenancy.oc1` ã‹ã‚‰å§‹ã¾ã‚Šã¾ã™.

2. User OCID
[ã“ã“](https://cloud.oracle.com/identity/domains/my-profile)ã‹ã‚‰å–å¾—ã—ã¦ãã ã•ã„.
`ocid1.user.oc1` ã‹ã‚‰å§‹ã¾ã‚Šã¾ã™.

3. Fingerprint
[ã“ã“](https://cloud.oracle.com/identity/domains/my-profile/api-keys?region=ap-osaka-1)ã‹ã‚‰å–å¾—ã—ã¦ãã ã•ã„.
`xx:xx:xx...xx` ã®ã‚ˆã†ãªãƒ•ã‚©ãƒ¼ãƒãƒƒãƒˆã§ã™.

4. Region
æ±äº¬ãƒªãƒ¼ã‚¸ãƒ§ãƒ³ãªã‚‰ `ap-tokyo-1`ã€å¤§é˜ªãƒªãƒ¼ã‚¸ãƒ§ãƒ³ãªã‚‰ `ap-osaka-1` ã§ã™.

terraform ãƒ•ã‚¡ã‚¤ãƒ«ã‚’å®Ÿè¡Œã™ã‚‹ç’°å¢ƒã‹ã‚‰ä»¥ä¸‹ã®æƒ…å ±ã‚’å–å¾—ã—ã¾ã™.

1. Private Key Path
å…ˆã»ã©ä½œæˆã—ãŸç§˜å¯†éµã®ãƒ‘ã‚¹ã‚’ãƒ¡ãƒ¢ã—ã¦ãã ã•ã„ã€è¨˜äº‹ã®é€šã‚Šã«å®Ÿè¡Œã—ãŸå ´åˆã¯ `~/.oci/oci_rsa.pem` ã¨ãªã‚Šã¾ã™.

# terraform ã®ãƒ—ãƒ­ãƒã‚¤ãƒ€ãƒ¼è¨­å®š

å…ˆã»ã©å–å¾—ã—ãŸæƒ…å ±ã‚’ä½¿ã£ã¦ oci provider ã®è¨­å®šã‚’ã—ã¾ã™.
è¨­å®šæƒ…å ±ã‚’ä¿å­˜ã™ã‚‹ `~/.oci/config` ã‚’ä½œæˆã—ã¦ä»¥ä¸‹ã®ã‚ˆã†ã«è¨˜è¼‰ã—ã¦ãã ã•ã„

```hcl:~/.oci/config
[DEFAULT]
tenancy_ocid="<tenancy-ocid>"
user_ocid="<user-ocid>"
private_key_path="<rsa-private-key-path>"
fingerprint="<fingerprint>"
region="<region-identifier>"
```

æ¬¡ã«ä»¥ä¸‹ã®ã‚ˆã†ãª hcl ã‚³ãƒ¼ãƒ‰ã‚’ä½œæˆã—ã¦ãã ã•ã„.

```hcl:provider.tf
terraform {
  required_providers {
    oci = {
      source = "oracle/oci"
    }
  }
}

provider "oci" {
  config_file_profile = "DEFAULT"
}
```

`terraform init` ã‚³ãƒãƒ³ãƒ‰ã‚’å®Ÿè¡Œã—ã¦ä»¥ä¸‹ã®ã‚ˆã†ã«è¡¨ç¤ºã•ã‚ŒãŸã‚‰è¨­å®šå®Œäº†ã§ã™.

```bash
â¯ terraform init
Initializing the backend...
Initializing provider plugins...
- Reusing previous version of oracle/oci from the dependency lock file
- Using previously-installed oracle/oci v4.123.0

Terraform has been successfully initialized!

You may now begin working with Terraform. Try running "terraform plan" to see
any changes that are required for your infrastructure. All Terraform commands
should now work.

If you ever set or change modules or backend configuration for Terraform,
rerun this command to reinitialize your working directory. If you forget, other
commands will detect it and remind you to do so if necessary.
```

# å¯ç”¨æ€§ãƒ‰ãƒ¡ã‚¤ãƒ³ã‚’å‡ºåŠ›ã—ã¦ã¿ã‚‹

å¯ç”¨æ€§ãƒ‰ãƒ¡ã‚¤ãƒ³ã‚’å–å¾—ã™ã‚‹ãŸã‚ã®ã‚·ãƒ³ãƒ—ãƒ«ãª hcl ã‚’ä½œæˆã—ã¾ã—ãŸ.

```hcl
terraform {
  required_providers {
    oci = {
      source = "oracle/oci"
    }
  }
}

provider "oci" {
  config_file_profile = "DEFAULT"
}

locals {
  oci_config_content = file("~/.oci/config")
  tenancy_ocid       = regex("(?m)^tenancy\\s*=\\s*(.*)$", local.oci_config_content)[0]
}

data "oci_identity_availability_domains" "ads" {
  compartment_id = local.tenancy_ocid
}

output "names" {
  value = [for ad in data.oci_identity_availability_domains.ads.availability_domains : ad.name]
}
```
:::details å®Ÿè¡Œçµæœ
```bash
â¯ terraform apply
data.oci_identity_availability_domains.ads: Reading...
data.oci_identity_availability_domains.ads: Read complete after 0s [id=IdentityAvailabilityDomainsDataSource-1575237231]

Changes to Outputs:
  + names = [
      + "ewah:AP-OSAKA-1-AD-1",
    ]

You can apply this plan to save these new output values to the Terraform state, without changing any real infrastructure.

Do you want to perform these actions?
  Terraform will perform the actions described above.
  Only 'yes' will be accepted to approve.

  Enter a value: yes


Apply complete! Resources: 0 added, 0 changed, 0 destroyed.

Outputs:

names = [
  "ewah:AP-OSAKA-1-AD-1",
]
```
:::

# (ãŠã¾ã‘) nginx ã‚µãƒ¼ãƒãƒ¼ã‚’ã‚·ãƒ³ãƒ—ãƒ«ãªæ§‹æˆã§ãƒ‡ãƒ—ãƒ­ã‚¤ã—ã¦ã¿ã‚‹

æœ€å°é™ã®ã‚·ãƒ³ãƒ—ãƒ«ãªæ§‹æˆã§ nginx ã‚µãƒ¼ãƒãƒ¼ã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã—ã¦ã¿ã¾ã™.
`iptables -I INPUT -p TCP --dport 80 -j ACCEPT` ã‚’å®Ÿè¡Œã—ã¦ã„ã‚‹ç‚¹ãŒãƒã‚¤ãƒ³ãƒˆã§ã€**OCI ã§ã¯ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§ OS ã®ãƒ•ã‚¡ã‚¤ã‚¢ã‚¦ã‚©ãƒ¼ãƒ«ãŒè¨­å®šã•ã‚Œã¦ã„ã‚‹ãŸã‚æ˜ç¤ºçš„ã«è¨±å¯ã™ã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™**.
`terraform apply` ã™ã‚‹ã¨å‡ºåŠ›ã•ã‚Œã‚‹ URL ã«ã‚¢ã‚¯ã‚»ã‚¹ã™ã‚‹ã¨ nignx ã‚µãƒ¼ãƒãƒ¼ã«ã‚¢ã‚¯ã‚»ã‚¹ã§ãã‚‹ã“ã¨ãŒç¢ºèªã§ãã¾ã™.

ã¾ãŸã€ssh ã®è¨­å®šã‚‚ã—ã¦ã„ã‚‹ã®ã§ `ssh ubuntu@<ip addr> -i ~/.ssh/<key name>` ã§ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ã« ssh ã§æ¥ç¶šã™ã‚‹ã“ã¨ã‚‚ã§ãã¾ã™.

:::details terraform ãƒ•ã‚¡ã‚¤ãƒ«
```hcl
terraform {
  required_providers {
    oci = {
      source = "oracle/oci"
    }
  }
}

provider "oci" {
  config_file_profile = "DEFAULT"
}

locals {
  oci_config_content = file("~/.oci/config")
  tenancy_ocid       = regex("(?m)^tenancy\\s*=\\s*(.*)$", local.oci_config_content)[0]
}

# ã‚³ãƒ³ãƒ‘ãƒ¼ãƒˆãƒ¡ãƒ³ãƒˆã‚’ä½œæˆ
resource "oci_identity_compartment" "this" {
  compartment_id = local.tenancy_ocid
  name           = "terraform"
  description    = "Terraform compartment"
}

# å¯ç”¨æ€§ãƒ‰ãƒ¡ã‚¤ãƒ³ã‚’å–å¾—
data "oci_identity_availability_domains" "this" {
  compartment_id = local.tenancy_ocid
}

# VCN ã‚’ä½œæˆ
resource "oci_core_vcn" "this" {
  cidr_block     = "10.0.0.0/16"
  compartment_id = oci_identity_compartment.this.id
  display_name   = "nginx-vcn"
}

# IGW ã‚’ä½œæˆ
resource "oci_core_internet_gateway" "this" {
  compartment_id = oci_identity_compartment.this.id
  display_name   = "nginx-igw"
  vcn_id         = oci_core_vcn.this.id
}

# ãƒ«ãƒ¼ãƒˆãƒ†ãƒ¼ãƒ–ãƒ«ã‚’ä½œæˆ
resource "oci_core_route_table" "this" {
  compartment_id = oci_identity_compartment.this.id
  vcn_id         = oci_core_vcn.this.id
  display_name   = "nginx-rt"

  route_rules {
    destination       = "0.0.0.0/0"
    network_entity_id = oci_core_internet_gateway.this.id
  }
}

# ã‚µãƒ–ãƒãƒƒãƒˆã‚’ä½œæˆ
resource "oci_core_subnet" "this" {
  cidr_block        = "10.0.1.0/24"
  compartment_id    = oci_identity_compartment.this.id
  vcn_id            = oci_core_vcn.this.id
  route_table_id    = oci_core_route_table.this.id
  security_list_ids = [oci_core_security_list.nginx_security_list.id]
  display_name      = "nginx-subnet"
}

# ã‚»ã‚­ãƒ¥ãƒªãƒ†ã‚£ãƒªã‚¹ãƒˆã®æ›´æ–°
resource "oci_core_security_list" "nginx_security_list" {
  compartment_id = oci_identity_compartment.this.id
  vcn_id         = oci_core_vcn.this.id
  display_name   = "nginx-security-list"

  egress_security_rules {
    destination = "0.0.0.0/0"
    protocol    = "all"
  }

  ingress_security_rules {
    protocol = "6" # TCP
    source   = "0.0.0.0/0"

    tcp_options {
      max = "22"
      min = "22"
    }
  }

  ingress_security_rules {
    protocol = "6" # TCP
    source   = "0.0.0.0/0"
    tcp_options {
      max = "80"
      min = "80"
    }
  }
}

# Compute ã‚¤ãƒ³ã‚¹ã‚¿ãƒ³ã‚¹ã‚’ä½œæˆ
resource "oci_core_instance" "nginx_instance" {
  availability_domain = data.oci_identity_availability_domains.this.availability_domains[0].name
  compartment_id      = oci_identity_compartment.this.id
  shape               = "VM.Standard.A1.Flex"

  shape_config {
    ocpus         = 1
    memory_in_gbs = 6
  }

  create_vnic_details {
    subnet_id        = oci_core_subnet.this.id
    assign_public_ip = true
  }

  source_details {
    source_type = "image"
    source_id   = data.oci_core_images.ubuntu_image.images[0].id
  }

  metadata = {
    ssh_authorized_keys = file("~/.ssh/oci-nginx.pub") # å…¬é–‹éµã¯äº‹å‰ã«ä½œæˆã—ã¦ãŠã
    user_data = base64encode(<<-EOF
    #!/bin/bash
    apt-get update
    apt-get install -y nginx
    systemctl start nginx
    systemctl enable nginx
    iptables -I INPUT -p TCP --dport 80 -j ACCEPT
    EOF
    )
  }
}

data "oci_core_images" "ubuntu_image" {
  compartment_id           = oci_identity_compartment.this.id
  operating_system         = "Canonical Ubuntu"
  operating_system_version = "24.04"
  shape                    = "VM.Standard.A1.Flex"
}

output "public_ip" {
  value = "http://${oci_core_instance.nginx_instance.public_ip}"
}

```
:::
# å‚è€ƒè¨˜äº‹

- https://docs.oracle.com/en-us/iaas/developer-tutorials/tutorials/tf-provider/01-summary.htm
- https://docs.oracle.com/en-us/iaas/Content/terraform/configuring.htm
